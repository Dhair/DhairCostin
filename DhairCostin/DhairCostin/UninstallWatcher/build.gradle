apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            assets.srcDirs = [
                    'src/main/assets',
                    'src/main/assets/armeabi'
            ]
        }
    }
}
//清理
task clearSource(type: Delete) {
    delete file('src/main/assets')
}
//打包
task ndkBuild(type: Exec, dependsOn: clearSource) {
    logger.log(LogLevel.ERROR, 'NdkBuild')
    def ndkDir = project.plugins.findPlugin('com.android.library').sdkHandler.getNdkFolder()
    commandLine "$ndkDir/ndk-build", '-C', 'src/main/jni',
            "NDK_OUT=$buildDir/ndk/obj",
            "NDK_APP_DST_DIR=$buildDir/ndk/libs/\$(TARGET_ARCH_ABI)"
}

task productSource(type: Copy, dependsOn: ndkBuild) {
    logger.log(LogLevel.ERROR, 'CopyExeFile')
    from fileTree(dir: file(buildDir.absolutePath + '/ndk/libs/armeabi'), include: '*')
    into file('src/main/assets/armeabi')
}

//执行 :'./gradlew productSource' 即可